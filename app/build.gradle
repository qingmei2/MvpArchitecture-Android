apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    defaultConfig {
        applicationId rootProject.ext.android["applicationId"]
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'tier'

    productFlavors {
        staging {
            dimension 'tier'
        }
        production {
            dimension 'tier'
        }
    }
    dataBinding {
        enabled = true
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("staging") && variant.buildType.name == "release") {
            setIgnore(true)
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["appcompat-v7"]
    //Junit
    testImplementation rootProject.ext.dependencies["junit"]
    //espresso
    androidTestImplementation(rootProject.ext.dependencies["espresso-core"]) {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestImplementation(rootProject.ext.dependencies["espresso-test-runner"]) {
        exclude module: 'support-annotations'
    }
    //mockito
    testImplementation rootProject.ext.dependencies["mockito-kotlin"]
    //robolectric
    testImplementation rootProject.ext.dependencies["robolectric"]
    testImplementation rootProject.ext.dependencies["robolectric-v4"]

    implementation rootProject.ext.dependencies["router"]
    annotationProcessor rootProject.ext.dependencies["router-compiler"]
    //dagger2
    implementation rootProject.ext.dependencies["dagger-android"]
    implementation rootProject.ext.dependencies["dagger-android-support"]
    annotationProcessor rootProject.ext.dependencies["dagger-compiler"]
    annotationProcessor rootProject.ext.dependencies["dagger-android-processor"]

    if (isBuildModule.toBoolean()) {
        implementation project(':module_core')
    } else {
        implementation project(':module_a')
    }

}
repositories {
    mavenCentral()
}
